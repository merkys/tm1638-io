#!/usr/bin/perl

use strict;
use warnings;

my $piece = random_piece();
my $level = 0;

my $field = [ [ 0, 0, 0, 0, 0, 0, 0, 0 ], [ 0, 0, 0, 0, 0, 0, 0, 0 ] ];

my $binary = 0;

while( <> ) {
    if( /^l/i ) {
        $piece = rotL( $piece );
    }
    if( /^r/i ) {
        $piece = rotR( $piece );
    }
    if( /^d/i ) {
        if( !$piece->[0][1] && !$piece->[1][1] ) {
            $piece = [ [ 0, $piece->[0][0] ], [ 0, $piece->[1][0] ] ];
        }
        my $i;
        for( $i = 0; $i < 6; $i++ ) {
            next if !$field->[0][$i+2] && !$field->[1][$i+2];
            last if $piece->[0][1] && $field->[0][$i+2];
            last if $piece->[1][1] && $field->[1][$i+2];
        }
        $field->[0][$i]   = 1 if $piece->[0][0];
        $field->[0][$i+1] = 1 if $piece->[0][1];
        $field->[1][$i]   = 1 if $piece->[1][0];
        $field->[1][$i+1] = 1 if $piece->[1][1];
        # reduce( $field );
        $piece = random_piece();
    }
    display( $field, $piece, $level, $binary );
}

sub rotL
{
    my( $piece ) = @_;
    return [
        [ $piece->[0][1], $piece->[1][1] ],
        [ $piece->[0][0], $piece->[1][0] ],
    ];
}

sub rotR
{
    my( $piece ) = @_;
    return [
        [ $piece->[1][0], $piece->[0][0] ],
        [ $piece->[1][1], $piece->[0][1] ],
    ];
}

sub reduce
{
    my( $field ) = @_;
    my $src  = 7;
    my $dest = 7;
    while( $src >= 0 ) {
        last if !$field->[0][$src] && !$field->[1][$src];
        if( $field->[0][$src] && $field->[1][$src] ) {
            $src--;
            next;
        }
        if( $src != $dest ) {
            $field->[0][$dest] = $field->[0][$src];
            $field->[1][$dest] = $field->[1][$src];
        }
        $src--;
        $dest--;
    }
}

sub display
{
    my( $field, $piece, $level, $binary ) = @_;
    if( $piece ) {
        if( $binary ) {
            print '00000000 ' x $level;
            for (0..1) {
                my $val = 0;
                $val = $val | 0x63 if $piece->[0][$_]; # top piece
                $val = $val | 0x5c if $piece->[1][$_]; # bottom piece
                printf( '%08b ', $val );
            }
            for ($level..7) {
                my $val = 0;
                $val = $val | 0x63 if $field->[0][$_]; # top piece
                $val = $val | 0x5c if $field->[1][$_]; # bottom piece
                printf( '%08b ', $val );
            }
        } else {
            print ' ' x $level . join( '', @{$piece->[0]} );
            print join( '', @{$field->[0]}[$level..7] ), "\n";
            print ' ' x $level . join( '', @{$piece->[1]} );
            print join( '', @{$field->[1]}[$level..7] ), "\n";
        }
    } else {
        if( $binary ) {
            for (0..7) {
                my $val = 0;
                $val = $val | 0x63 if $field->[0][$_]; # top piece
                $val = $val | 0x5c if $field->[1][$_]; # bottom piece
                printf( '%08b ', $val );
            }
        } else {
            print join( '', @{$field->[0]} ), "\n";
            print join( '', @{$field->[1]} ), "\n";
        }
    }
    print "\n" if $binary;
}

sub random_piece
{
    my $piece = [ [ 1, 0 ], [ 0, 0 ] ];
    $piece->[0][1] += (rand() > 0.5);
    $piece->[1][0] += (rand() > 0.5);
    $piece->[1][1] += (rand() > 0.5);

    for (0..int(rand()*4)) {
        $piece = rotL( $piece );
    }
    return $piece;
}
